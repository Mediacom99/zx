const Self = @This();
const std = @import("std");
const unicode = @import("unicode.zig");

/// In case of utf8-encoded unicode input this slice is the u8
/// view of an original i32 slice containing all the input's codepoints.
/// If input is only ascii this slice contains each character directly as u8.
slice: []const u8,

is_ascii: bool,

trim_len_known: bool = false,

trim_len: u16 = 0,

//TODO: UNDERSTAND THIS
// XXX Piggybacking item index here is a horrible idea. But I'm trying to
// minimize the memory footprint by not wasting padded spaces.
index: u32 = 0,

const overflow64: u64 = 0x8080808080808080;
const overflow32: u32 = 0x80808080;
/// Very fast ascii check. Returns whether ascii or not and
/// the index of the first byte from which it is not
/// be possible to tell if the input is ascii.
fn isAsciiOptimized(bytes: []const u8) struct { bool, usize } {
    const len = bytes.len;
    var i: usize = 0;

    //Loop over 8 byte chunk
    while (i + 8 <= len) {
        const eight_bytes = @as(*const [8]u8, @ptrCast(bytes[i .. i + 8].ptr));
        const chunk: u64 = std.mem.readInt(u64, eight_bytes, .little);
        //If not zero it means at least 1 out of 8 bytes is not ascii
        if ((overflow64 & chunk) != 0) {
            return .{ false, i };
        }
        i += 8;
    }

    //Loop over remaining 4 bytes chunk
    while (i + 4 <= len) {
        const four_bytes = @as(*const [4]u8, @ptrCast(bytes[i .. i + 4].ptr));
        const chunk = std.mem.readInt(u32, four_bytes, .little);
        //If not zero it means at least 1 out of 4 bytes is not ascii
        if ((overflow32 & chunk) != 0) {
            return .{ false, i };
        }
        i += 4;
    }

    //Check single bytes
    while (i < bytes.len) {
        if (!std.ascii.isASCII(bytes[i])) {
            return .{ false, i };
        }
        i += 1;
    }
    return .{ true, 0 };
}

/// Wraps byte slice into Chars: if input is only ascii the original slice is used.
/// If input is not only ascii then every unicode codepoint is stored as i32 type
/// called Rune.
/// Chars always contains a []u8 view of the original []i32 slice;
/// Input is assumed to be valid utf-8 with replacement chars if needed.
/// Always call deinit in order to free owned runes in case of unicode input.
pub fn initFromByteSlice(alloc: std.mem.Allocator, bytes: []const u8) !Self {
    const is_ascii, const ascii_until = isAsciiOptimized(bytes);
    if (is_ascii) {
        return .{ .slice = bytes, .is_ascii = true };
    }

    //Not only ascii, first we append the ascii we have
    var runes = std.ArrayList(i32).init(alloc);
    errdefer runes.deinit();

    for (0..ascii_until) |i| {
        //Cast ascii byte as i32
        try runes.append(@as(i32, @intCast(bytes[i])));
    }

    // bytes is assumed to be valid utf8 so we can iterate over codepoints
    // FIXME that bytes slice IS WRONG
    var utf8_iter = std.unicode.Utf8Iterator{ .bytes = bytes[ascii_until..], .i = 0 };
    while (utf8_iter.nextCodepoint()) |codepoint| {
        try runes.append(@intCast(codepoint));
    }

    const runes_owned: []i32 = try runes.toOwnedSlice();

    //This is smart. (All credits goes to fzf).
    //Cast owned runes slice as u8 slice
    const bytes_ptr: [*]u8 = @alignCast(@ptrCast(runes_owned.ptr));
    const bytes_len: usize = runes_owned.len * @sizeOf(i32);
    return .{
        .slice = bytes_ptr[0..bytes_len],
        .is_ascii = false,
    };
}

/// Detects if input is unicode and only in that case
/// it frees the internal slice. For ascii input
/// no other allocation was done by Chars because each
/// original byte was enough to store the input, so
/// there is nothing to free.
pub fn deinit(self: *Self, alloc: std.mem.Allocator) void {
    if (!self.is_ascii) {
        if (self.optional_runes()) |runes| {
            alloc.free(@constCast(runes));
        }
    }
}

/// Casts back from u8 to original rune slice (i32).
/// Returns null if internal slice is ascii only.
pub fn optional_runes(self: Self) ?[]const i32 {
    if (self.is_ascii) {
        return null;
    }
    const runes_ptr: [*]const i32 = @alignCast(@ptrCast(self.slice.ptr));
    const runes_len = self.slice.len / @sizeOf(i32);
    return runes_ptr[0..runes_len];
}

/// Returns number of runes for unicode
/// input or length of internal slice for
/// ascii input.
pub fn length(self: Self) usize {
    if (self.optional_runes()) |runes| {
        return runes.len;
    }
    return self.slice.len;
}

test "fromBytes ascii-only input" {
    const allocator = std.testing.allocator;
    var chars = try Self.initFromByteSlice(allocator, "Hello World!\n\t\r");
    defer chars.deinit(allocator);
    try std.testing.expectEqual(chars.is_ascii, true);
    try std.testing.expectEqual(chars.optional_runes(), null);
    try std.testing.expectEqual(chars.length(), 15);
}

test "fromBytes comprehensive Unicode string" {
    const alloc = std.testing.allocator;
    const test_unicode =
        // ASCII start (46 bytes)
        "The quick brown fox jumps over the lazy dog! " ++
        // Latin extended (35 bytes - includes multi-byte chars)
        "CafÃ©, naÃ¯ve, rÃ©sumÃ©, ZÃ¼rich, seÃ±or, " ++
        // Greek (35 bytes)
        "Î•Î»Î»Î·Î½Î¹ÎºÎ¬ (Greek): Î‘Î»Ï†Î¬Î²Î·Ï„Î¿, " ++
        // Cyrillic (47 bytes)
        "Ð ÑƒÑÑÐºÐ¸Ð¹ ÑÐ·Ñ‹Ðº (Russian): ÐŸÑ€Ð¸Ð²ÐµÑ‚ Ð¼Ð¸Ñ€! " ++
        // Chinese (39 bytes)
        "ä¸­æ–‡ (Chinese): ä½ å¥½ä¸–ç•Œï¼æ˜¥å¤ç§‹å†¬ï¼Œ" ++
        // Japanese (48 bytes)
        "æ—¥æœ¬èªž: ã²ã‚‰ãŒãªã€ã‚«ã‚¿ã‚«ãƒŠã€æ¼¢å­—ã€" ++
        // Korean (30 bytes)
        "í•œêµ­ì–´ (Korean): ì•ˆë…•í•˜ì„¸ìš”! " ++
        // Arabic (34 bytes)
        "Ø§Ù„Ø¹Ø±Ø¨ÙŠØ©: Ù…Ø±Ø­Ø¨Ø§ Ø¨Ø§Ù„Ø¹Ø§Ù„Ù…! " ++
        // Hebrew (26 bytes)
        "×¢×‘×¨×™×ª: ×©×œ×•× ×¢×•×œ×! " ++
        // Thai (35 bytes)
        "à¸ à¸²à¸©à¸²à¹„à¸—à¸¢: à¸ªà¸§à¸±à¸ªà¸”à¸µà¸„à¸£à¸±à¸š " ++
        // Emojis (49 bytes - 4 bytes each + space)
        "Emojis: ðŸ˜€ðŸ˜ƒðŸ˜„ðŸ˜ðŸ¤£ðŸ˜‚ðŸ˜ŠðŸ˜‡ðŸ™‚ðŸ™ƒðŸ˜‰ðŸ˜Œ " ++
        // More content...
        "END";
    var chars = try Self.initFromByteSlice(alloc, test_unicode);
    defer chars.deinit(alloc);
    try std.testing.expect(!chars.is_ascii);
    const runes = chars.optional_runes() orelse unreachable;

    // Check ASCII part
    try std.testing.expectEqual('T', runes[0]);
    try std.testing.expectEqual('h', runes[1]);
    try std.testing.expectEqual('e', runes[2]);
    try std.testing.expectEqual(' ', runes[3]);

    // Find where non-ASCII starts - should be at "CafÃ©"
    // The 'Ã©' in CafÃ© is at position 49 (after the first sentence + "Caf")
    var ascii_end: usize = 0;
    for (0..runes.len) |i| {
        if (runes[i] > 127) {
            ascii_end = i;
            break;
        }
    }

    // The first non-ASCII character should be 'Ã©' (U+00E9)
    try std.testing.expectEqual(0x00E9, runes[ascii_end]);

    // Find "Î•Î»Î»Î·Î½Î¹ÎºÎ¬" - Greek capital epsilon
    var greek_start: ?usize = null;
    for (0..runes.len) |i| {
        if (runes[i] == 0x0395) { // Î• (Greek capital epsilon)
            greek_start = i;
            break;
        }
    }
    try std.testing.expect(greek_start != null);

    // Check Greek word "Î•Î»Î»Î·Î½Î¹ÎºÎ¬"
    if (greek_start) |start| {
        try std.testing.expectEqual(@as(i32, 0x0395), runes[start]); // Î•
        try std.testing.expectEqual(@as(i32, 0x03BB), runes[start + 1]); // Î»
        try std.testing.expectEqual(@as(i32, 0x03BB), runes[start + 2]); // Î»
        try std.testing.expectEqual(@as(i32, 0x03B7), runes[start + 3]); // Î·
        try std.testing.expectEqual(@as(i32, 0x03BD), runes[start + 4]); // Î½
        try std.testing.expectEqual(@as(i32, 0x03B9), runes[start + 5]); // Î¹
        try std.testing.expectEqual(@as(i32, 0x03BA), runes[start + 6]); // Îº
        try std.testing.expectEqual(@as(i32, 0x03AC), runes[start + 7]); // Î¬
    }

    // Find Chinese "ä¸­æ–‡"
    var chinese_start: ?usize = null;
    for (0..runes.len) |i| {
        if (runes[i] == 0x4E2D) { // ä¸­
            chinese_start = i;
            break;
        }
    }
    try std.testing.expect(chinese_start != null);
    if (chinese_start) |start| {
        try std.testing.expectEqual(0x4E2D, runes[start]); // ä¸­
        try std.testing.expectEqual(0x6587, runes[start + 1]); // æ–‡
    }

    // Find first emoji
    var emoji_start: ?usize = null;
    for (0..runes.len) |i| {
        if (runes[i] == 0x1F600) { // ðŸ˜€
            emoji_start = i;
            break;
        }
    }
    try std.testing.expect(emoji_start != null);
    if (emoji_start) |start| {
        try std.testing.expectEqual(0x1F600, runes[start]); // ðŸ˜€
        try std.testing.expectEqual(0x1F603, runes[start + 1]); // ðŸ˜ƒ
        try std.testing.expectEqual(0x1F604, runes[start + 2]); // ðŸ˜„
    }

    // Check that END is at the end
    const len = runes.len;
    try std.testing.expectEqual('E', runes[len - 3]);
    try std.testing.expectEqual('N', runes[len - 2]);
    try std.testing.expectEqual('D', runes[len - 1]);
}
